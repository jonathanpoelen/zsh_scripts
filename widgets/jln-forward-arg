#!/usr/bin/env zsh

# Moves to the start of the next argument
# supports NUMERIC

emulate -L zsh -o extendedglob

local words pat

# move cursor to the beginning of the parameter
CURSOR=${#LBUFFER%%${${(Z:n:)LBUFFER}[-1]}}

words=(${(Z:n:)RBUFFER})

if (( NUMERIC > 1 )); then
  local -i numeric=$NUMERIC i
  # between space, decrement the counter to consider spaces as a parameter
  if [[ $BUFFER[$CURSOR+1] = [[:space:]] ]]; then
    ((--numeric))
  fi

  if (( numeric >= $#words )); then
    CURSOR=$#BUFFER
  else
    for (( i=1; i <= numeric; ++i )); do
      pat+='${words['$i']}[[:space:]]#'
    done
    eval CURSOR+='${#RBUFFER}-${#RBUFFER##[[:space:]]#'$pat'}'
  fi
# between space, skip spaces
elif [[ $BUFFER[$CURSOR+1] = [[:space:]] ]]; then
  CURSOR+=${#RBUFFER}-${#RBUFFER##[[:space:]]#}
# skip current parameter and spaces
else
  CURSOR+=${#RBUFFER}-${#RBUFFER##${words[1]}[[:space:]]#}
fi
