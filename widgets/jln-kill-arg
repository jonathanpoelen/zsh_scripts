#!/usr/bin/env zsh

# Remove the following argument.
# If the cursor is on an argument, it will be truncated.
# supports NUMERIC

emulate -L zsh -o extendedglob

local words lwords
local -i skip iword

words=(${(Z:n:)BUFFER})
lwords=(${(Z:n:)LBUFFER})

iword=$#lwords
skip=$((${#words[$#lwords]} - ${#lwords[-1]}))

# in not the middle of a shell word
if (( skip == 0 )); then
  # between two shell words
  if [[ $RBUFFER = [[:space:]]* ]]; then
    skip=$#RBUFFER-${#RBUFFER##[[:space:]]#}+${#words[$#lwords+1]}
  # on the start of a shell word
  else
    skip=$#words[$#lwords+1]
  fi
  ((++iword))
fi

if (( NUMERIC > 1 )); then
  local -i numeric=NUMERIC+iword
  local pat=${(q)RBUFFER[0,skip]}
  if (( numeric > $#words )); then
    numeric=$#words+1
  fi
  for ((iword+=1; iword<numeric; ++iword)); do
    pat+='[[:space:]]#'${(q)words[iword]}
  done
  eval skip='$#RBUFFER-${#RBUFFER##'$pat'}'
fi

RBUFFER=${RBUFFER:$skip}
